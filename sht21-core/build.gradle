plugins {
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.7.3'
}

version = '1.0.5-SNAPSHOT'

dependencies {
	implementation 'com.pi4j:pi4j-core:1.1'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.2'
	testRuntimeOnly'org.junit.jupiter:junit-jupiter-engine:5.0.2'
	testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'
}

tasks.withType(JavaCompile) {
	options.fork = true
	options.incremental = true
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
}

def pomConfig = {
	inceptionYear '2014'
	licenses {
		license {
			name "AGPL"
			url "https://github.com/stefanfreitag/SHT21/blob/master/LICENSE"
			distribution "repo"
		}
	}

	scm {
		url 'scm:git:https://github.com/stefanfreitag/SHT21'
		connection 'scm:git:https://github.com/stefanfreitag/SHT21'
		developerConnection 'scm:git:https://github.com/stefanfreitag/SHT21'
	}
	developers {
		developer {
			id "sfreitag"
			name "Stefan Freitag"
			email "stefan@stefreitag.de"
			timezone "Europe/Berlin"
			roles {
				role "developer"
			}
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			//            artifact sourcesJar
			//            artifact javadocJar

			groupId 'de.freitag.stefan'
			pom.withXml {
				def root = asNode()
				root.appendNode('description', '')
				root.children().last() + pomConfig
			}
		}
	}
}

bintrayUpload.dependsOn(build)

bintray {
	if (!project.hasProperty('bintray_user')) {
		project.ext.bintray_user = 'default'
	}
	if (!project.hasProperty('bintray_api_key')) {
		project.ext.bintray_api_key = 'default'
	}

	user = "$bintray_user"
	key = "$bintray_api_key"

	publications = ['mavenJava']

	dryRun = false
	publish = true
	pkg {
		repo = 'maven'
		name = 'SHT21'
		desc = 'Java library for communication with an SHT21 sensor'
		websiteUrl = 'https://github.com/stefanfreitag/SHT21'
		issueTrackerUrl = 'https://github.com/stefanfreitag/SHT21/issues'
		vcsUrl = 'git://github.com/stefanfreitag/SHT21.git'
		licenses = ['AGPL']
		labels = ['Java', 'SHT21']
		publicDownloadNumbers = true
		version {
			name = project.version
			vcsTag = project.version
			gpg {
				sign = true
				passphrase = project.hasProperty('gpg_passphrase') ? gpg_passphrase :  ''
			}
		}

	}
}

compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

jar {
	manifest {
		attributes 'Implementation-Title': 'SHT21 Core Library', 'Implementation-Version': archiveVersion
	}
	exclude('log4j2.xml')
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

javadoc {
	options.addBooleanOption('html5', true)
}